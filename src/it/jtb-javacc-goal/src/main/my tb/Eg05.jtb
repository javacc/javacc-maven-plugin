options
{
  // relative path of Java files generated by JavaCC from the JJ file
  OUTPUT_DIRECTORY = "../../../target/gen-src/my cc";
  // out file (default jtb.out.jj)
  JTB_O = "../../../target/gen-src/my tb/Eg05.jj";
  // syntaxtree & visitor directory
  JTB_D = "../../../target/gen-src/my tb";
  // package name
  JTB_P = "org.jtb_jcc.eg05";
  // nodes package (default syntaxtree)
  JTB_NP = "ast";
  // nodes directory (default syntaxtree)
  JTB_ND = "ast";
  // visitors package (default visitor)
  JTB_VP = "vis";
  // visitors directory (default visitor)
  JTB_VD = "vis";
  // signature package (default visitor.signature)
  JTB_SIGP = "vis.sig";
  // signature directory (default visitor/signature)
  JTB_SIGD = "vis/sig";
  // create the visitors IGenVisitor & DepthFirstGenVisitor with methods void visit(<prod> n, String argu)
  JTB_VIS = "Gen,void,java.lang.String";
}

PARSER_BEGIN(Eg05)

package org.jtb_jcc.eg05;

import org.jtb_jcc.eg05.vis.*;

/** An Arithmetic Grammar. */
public class Eg05
{
  /** Main entry point. */
  public static void main(String args [])
  {
    System.err.flush();
    System.out.println("Reading from standard input (JTB version)...");
    Eg05 t = new Eg05(System.in);
    try
    {
      DumpVisitor v = new DumpVisitor();
      Start n = t.Start();
      n.accept(v, "");
      System.out.println("Thank you.");
      System.exit(0);
    }
    catch (Exception e)
    {
      System.out.flush();
      System.err.println("Oops.");
      System.err.println(e.getMessage());
      e.printStackTrace();
      System.exit(-1);
    }
  }
}

PARSER_END(Eg05)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
| < "/*" (~[ "*" ])* "*"
    (
      ~[ "/" ] (~[ "*" ])* "*"
    )*
    "/" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL :
    < DECIMAL_LITERAL > ([ "l", "L" ])?
  | < HEX_LITERAL > ([ "l", "L" ])?
  | < OCTAL_LITERAL > ([ "l", "L" ])? 
  >
| 
  < #DECIMAL_LITERAL : [ "1"-"9" ] ([ "0"-"9" ])* >
| 
  < #HEX_LITERAL : "0" [ "x", "X" ] ([ "0"-"9", "a"-"f", "A"-"F" ])+ >
| 
  < #OCTAL_LITERAL : "0" ([ "0"-"7" ])* >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER :
    < LETTER >
    (
      < LETTER >
    | < DIGIT >
    )* >
| 
  < #LETTER : [ "_", "a"-"z", "A"-"Z" ] >
| 
  < #DIGIT : [ "0"-"9" ] >
}

/** Main production. */
void Start() :
{}
{
  Expression() ";"
}

/** An Expression. */
void Expression() :
{}
{
  AdditiveExpression()
}

/** An Additive Expression. */
void AdditiveExpression() :
{}
{
  MultiplicativeExpression()
  (
    (
      "+"
    | "-"
    )
    MultiplicativeExpression()
  )*
}

/** A Multiplicative Expression. */
void MultiplicativeExpression() :
{}
{
  UnaryExpression()
  (
    (
      "*"
    | "/"
    | "%"
    )
    UnaryExpression()
  )*
}

/** A Unary Expression. */
void UnaryExpression() :
{}
{
  "(" Expression() ")"
| MyID()
  {
    ((Token) n5.f0).image = "reset";
  }
| Integer_()
}

/** An Identifier. */
void MyID() :
{
  Token t = null;
}
{
  t = < IDENTIFIER >
  {
    ((Token) n0).image = t.image + " modified";
  }
}

/** An Integer. */
void Integer_() :
{}
{
  < INTEGER_LITERAL >
}
