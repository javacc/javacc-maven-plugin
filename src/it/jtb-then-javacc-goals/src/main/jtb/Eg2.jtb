options
{
  // relative path of Java files generated by JavaCC from the JJ file
  OUTPUT_DIRECTORY = "../../../target/generated-sources/javacc";
  // out file (default jtb.out.jj)
  JTB_O = "../../../target/generated-sources/jtb/Eg2.jj";
  // syntaxtree & visitor directory
//  JTB_D = "../../../target/generated-sources/jtb";
  // package name
//  JTB_P = "org.jtb_th_jcc.eg2";
  // node prefix
  JTB_NPFX = "AST"; // nodes prefix
  // create the visitors IGenVisitor & DepthFirstGenVisitor with methods void visit(<prod> n, String argu)
  JTB_VIS = "Gen,void,java.lang.String";
  // generate javadoc comments (default false)
  JTB_JD = true;
  // store special tokens in the nodes (default false)
  JTB_TK = true;
  // node scope hooks (default false)
  JTB_HK = true;
  // no overwrite (default false): to keep the modified ASTMyID.java
//  JTB_W = true;
}

PARSER_BEGIN(Eg2)

// no declared package and no option package, so no sub packages for visitors, nodes...

/** An Arithmetic Grammar. */
public class Eg2
{
  /** Main entry point. */
  public static void main(String args [])
  {
    System.err.flush();
    System.out.println("Reading from standard input (JTB version)...");
    Eg2 t = new Eg2(System.in);
    try
    {
      DepthFirstGenVisitor v = new DepthFirstGenVisitor();
      ASTStart n = t.ASTStart();
      n.accept(v, "");
      System.out.println("Thank you.");
      System.exit(0);
    }
    catch (Exception e)
    {
      System.out.flush();
      System.err.println("Oops.");
      System.err.println(e.getMessage());
      e.printStackTrace();
      System.exit(-1);
    }
  }
}

PARSER_END(Eg2)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
| < "/*" (~[ "*" ])* "*"
    (
      ~[ "/" ] (~[ "*" ])* "*"
    )*
    "/" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL :
    < DECIMAL_LITERAL > ([ "l", "L" ])?
  | < HEX_LITERAL > ([ "l", "L" ])?
  | < OCTAL_LITERAL > ([ "l", "L" ])? 
  >
| 
  < #DECIMAL_LITERAL : [ "1"-"9" ] ([ "0"-"9" ])* >
| 
  < #HEX_LITERAL : "0" [ "x", "X" ] ([ "0"-"9", "a"-"f", "A"-"F" ])+ >
| 
  < #OCTAL_LITERAL : "0" ([ "0"-"7" ])* >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER :
    < LETTER >
    (
      < LETTER >
    | < DIGIT >
    )* >
| 
  < #LETTER : [ "_", "a"-"z", "A"-"Z" ] >
| 
  < #DIGIT : [ "0"-"9" ] >
}

/** Main production. */
void Start() :
{}
{
  Expression() ";"
}

/** An Expression. */
void Expression() :
{}
{
  AdditiveExpression()
}

/** An Additive Expression. */
void AdditiveExpression() :
{}
{
  MultiplicativeExpression()
  (
    (
      "+"
    | "-"
    )
    MultiplicativeExpression()
  )*
}

/** A Multiplicative Expression. */
void MultiplicativeExpression() :
{}
{
  UnaryExpression()
  (
    (
      "*"
    | "/"
    | "%"
    )
    UnaryExpression()
  )*
}

/** A Unary Expression. */
void UnaryExpression() :
{}
{
  "(" Expression() ")"
| MyID()
  {
    ((Token) n5.f0).image = "reset";
  }
| Integer()
}

/** An Identifier. */
void MyID() :
{
  Token t = null;
}
{
  t = < IDENTIFIER >
  {
    ((Token) n0).image = t.image + " modified";
  }
}

/** An Integer. */
void Integer() :
{}
{
  < INTEGER_LITERAL >
}
